
def read_ints(?is:list(int)) use !io {
    ?is = []
    do {
        !read(?i)
        until i < 0
        ?is = [i | is]
    }
    reverse(is, ?is)
}

def {test} `[]`(as:list(A), idx:int, ?a:A) {
    if { idx < 0 :: fail }
    [?a | ?as] = as
    if { idx > 0 :: ?a = as[idx - 1] }
}

def fold_window(n:int, f:(A,A,!S), as:list(A), !s:S) {
    if { as = [?a0 | ?as] & as[n] = ?a1 :: 
        f(a0, a1, !s)
        fold_window(n, f, as, !s)
    }
}

!read_ints(?depths)

?count = 0
fold_window(0, { if { @ < @ :: incr(!@) } }, depths, !count)
!print("Part 1: "); !println(count)

?count = 0
fold_window(2, { if { @ < @ :: incr(!@) } }, depths, !count)
!print("Part 2: "); !println(count)
